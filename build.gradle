description = "herokuapp application"

apply plugin: "war"
apply plugin: "java"
apply plugin: "jetty"
apply plugin: "application"
apply plugin: "idea"

mainClassName = "com.howardlewiship.tapx.heroku.JettyMain"
applicationName = "app"

sourceCompatibility = "1.5"
targetCompatibility = "1.5"

group = "org.example"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()

    // All things JBoss/Javassist/Hibernate
    mavenRepo urls: "http://repository.jboss.org/nexus/content/groups/public/"

    // For stable versions of the tapx libraries
    mavenRepo urls: "http://howardlewisship.com/repository/"

    // For non-stable versions of the tapx libraries
    mavenRepo urls: "http://howardlewisship.com/snapshot-repository/"

    // For access to Apache Staging (Preview) packages
    mavenRepo urls: "https://repository.apache.org/content/groups/staging"
}

// This simulates Maven's "provided" scope, until it is officially supported by Gradle
// See http://jira.codehaus.org/browse/GRADLE-784

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {

    compile "org.apache.tapestry:tapestry-core:5.3"

    // This adds automatic compression of JavaScript and CSS in production mode:
    compile "org.apache.tapestry:tapestry-yuicompressor:5.3"

    compile "com.howardlewisship:tapx-heroku:1.2-SNAPSHOT"

    // Uncomment this to add support for file uploads:
    // compile "org.apache.tapestry:tapestry-upload:5.3"

    provided "javax.servlet:servlet-api:2.5"
}

test {
    useTestNG()

    options.suites("src/test/conf/testng.xml")

    systemProperties["tapestry.service-reloading-enabled"] = "false"
    systemProperties["tapestry.execution-mode"] = "development"

    maxHeapSize = "600M"

    jvmArgs("-XX:MaxPermSize=256M")

    enableAssertions = true
}


ideaModule {
    scopes.PROVIDED.plus += configurations.provided
    javaVersion = "inherited"
}

task stage(dependsOn: ['installApp'])